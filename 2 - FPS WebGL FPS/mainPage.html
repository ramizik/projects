<!--
EXTRA CREDIT (All Implemented):
1. Wasd keys are used to move forward/back and left/right.
2. The mouse is used to look around the environment.
3. The game has clear objectives.
4. The game has win state.
5. The game has loss state.
6. Instructions for our game is in an alert.
-->

<!--
FileName:       index.html
Programmers: Ramis Hasanli, Wil Bauer, Vanessa Kan 
Programmer:     Dan Cliburn
Date:           8/25/2020
Purpose:		This file defines the html code necessary to run our WebGL 2 program.
I've made an attempt to separate the parts of the program using the Model-View-Controller 
design pattern. The model.js, view.js, and controller.js files define the code for those 
components respectively. I define the vertex and fragment shader code in this file.
For Lab 7 we also start loading the gl-matrix.js file to perform matrix operations.
-->
<!-- Load image files -->
<!-- <img src="arrow.bmp" id="arrow" hidden /> -->


<img src="floorText.bmp" id="floor" hidden />
<img src="wallText.bmp" id="wall" hidden />
<img src="npcTEXT.bmp" id="npc" hidden />
<img src="robot1.bmp" id="npc1" hidden />
<img src="robot2.bmp" id="npc2" hidden />



<html>
<head>
    <title>COMP 153 - Assignment 7</title>
    
    <style type="text/css">
        canvas {
            border: 5px solid black;
        }
    </style>

    <!-- load the javascript files used in the program-->
    <script src="model.js" defer></script>
    <script src="view.js" defer></script>
    <script src="controller.js" defer></script>
    <script src="ground.js" defer></script>
    <script src="walls.js" defer></script>
    <script src="rooms.js" defer></script>
    <script src="texsquare.js" defer></script>



    <script src="gl-matrix.js" defer></script> 

    <!-- Define the vertex and fragment shaders -->
    <script id="phong-tex-vertex-shader" type="shader.vert">
        #version 300 es //specifies that we want OpenGL ES 3.0
        //This vertex shader is based on Example 7.8 on pages 377 and 378 of the OpenGL Programming
        //Guide (8th edition) with some tweaks to support shininess as a vertex property. I also
        //modified the code somewhat to make it closer to the Phong Reflection Model discussed in class.
        //For lab 7 it was modified to allow the eye position to move around the scene. Lighting
        //calculations are done in "eye" coordinates, so position and normal must be calculated appropriately.

        layout(location = 0) in vec4 vertexPosition;
        layout(location = 1) in vec4 vertexColor;
        layout(location = 2) in vec3 vertexNormal;
        layout(location = 3) in float vertexShininess;
        layout(location = 4) in vec2 in_tex_coord; //texture coodinates are now being passed in as vertex attributes

        uniform mat4 modelMatrix;
        uniform mat4 viewMatrix;
        uniform mat4 projectionMatrix;

        out vec4 position; //position of the vertex in "eye" coordinates
        out vec4 color;
        out vec3 normal; //orientation of the normal in "eye" coordinates
        out float shininess;
        out vec2 vs_tex_coord; //out variable so that texture coordinates can be made available to the fragment shader

        void main()
        {
            //Assign the in variables (attributes) to out variables (varyings) so that
            //they can be accessed in the fragment shader for lighting calculations.
	        position = viewMatrix * modelMatrix * vertexPosition; //position in "eye" coordinates
	        color = vertexColor;
            //calculate the "normal matrix"
            mat4 modelViewMatrix = viewMatrix * modelMatrix;
            mat4 normalMatrix = transpose(inverse(modelViewMatrix));
            vec4 n = normalMatrix * vec4(vertexNormal, 0.0);
            normal = normalize(vec3(n.x, n.y, n.z)); //normalize just in case it is not a unit vector
	        shininess = vertexShininess;
            vs_tex_coord = in_tex_coord; //copy the texture coordinate passed into this shader to an out variable

            //Here the input vertexPosition is multiplied by the model, view, and
            //projection matrices to determine the final position of the vertex 
            gl_Position = projectionMatrix * viewMatrix * modelMatrix * vertexPosition;
        }
    </script>
    <script id="phong-tex-fragment-shader" type="shader.frag">
        #version 300 es //specifies that we want OpenGL ES 3.0
        precision highp float; //specifies "high precision" - necessary to access the viewMatrix here
        //This fragment shader blends the active texture with the fragment's color before lighting 
        //calculations are performed. For Lab 10 calculations for light attenuation have been removed. 

        //uniform variables
        uniform vec3 globalAmbientLight;
        uniform mat4 viewMatrix; //needed to transform the light positions into "eye" coordinates
        uniform vec4 light_color; //color of light
        uniform vec4 light_position; //location of light if w = 1, otherwise the direction towards the light
        uniform sampler2D tex_image; //Support for texture mapping

        in vec4 position; //position of the fragment in "eye" coordinates
        in vec4 color; //surface color at the fragment
        in vec3 normal; //normal vector at the fragment in "eye" coordinates
        in float shininess; //shininess of the surface at the fragment
        in vec2 vs_tex_coord;

        out vec4 frag_color; //resulting fragment color after lighting is applied

        void main()
        {
	        //Implement the Phong reflectance model
	        //initialize scatteredLight to the global ambience * fragment color
	        vec3 scatteredLight = globalAmbientLight*texture(tex_image, vs_tex_coord).rgb*color.rgb;
	        vec3 reflectedLight = vec3(0.0, 0.0, 0.0);
	        vec3 surfaceSpecularColor = vec3(1.0, 1.0, 1.0); //normally we want the specular color of the  
		    //surface to be white so that the color of the specular highlight is the color of the light.  
            //If we do not want specular highlights at all you can set this to black (0.0, 0.0, 0.0).
                                                    	
	        vec3 I = light_color.rgb;  //Intensity (color) of the light
	        vec3 L; //this will be the L vector in the Phong reflectance model
            vec4 lightPos_eyeCoords = viewMatrix*light_position; //put light_position in "eye" coordinates
            vec3 surfaceDiffuseColor = texture(tex_image, vs_tex_coord).rgb*color.rgb;
	        float f = 1.0; //Not light attenuation will be a calculated in this shader
				
	        if (light_position.w > 0.1) //a positional light
	        {
		        vec4 LTemp = lightPos_eyeCoords - position;  //Calculate the L vector
		        L = vec3(LTemp.x, LTemp.y, LTemp.z); //remove the w value from the L vector
		        float d = length(L); //d in the phong reflectance model
			    L = L / d; //normalize L
		    }
		    else //a directional light
	        {
                //remove w value to form the L vector and normalize
                L = normalize(vec3(lightPos_eyeCoords.x, lightPos_eyeCoords.y, lightPos_eyeCoords.z));
		    }

	        float diffuseModifier = max ( 0.0, dot(normalize(normal), L) );
		    float specularModifier;
	        if (diffuseModifier > 0.001) //light is shining directly on the object so there may be a specular highlight
            {
		        //Find the reflectance vector, r, and the vector towards the viewer, v
			    vec3 r = normalize( reflect (-L, normal) ); //find r by "reflecting" L about n
		        vec4 viewerPos = vec4(0.0, 0.0, 0.0, 1.0); //The viewer is at (0,0,0) in "eye" coordinates
			    vec4 vTemp = viewerPos - position;
			    vec3 v = normalize(vec3(vTemp.x, vTemp.y, vTemp.z)); //calculate v, the vector towards the viewer 
			    specularModifier = pow (max ( 0.0, dot(r, v)), shininess );
		    }
		    else //no specular highlight, light is probably behind the surface
		    {
		        specularModifier = 0.0;
		    }

		    //Accumulate all the light's effects
		    reflectedLight += f *		                                                //attenuation
							     (  (I * surfaceDiffuseColor * diffuseModifier) +		//diffuse term
							        (I * surfaceSpecularColor * specularModifier) );	//specular term

	        vec3 sumOfLights = scatteredLight + reflectedLight;
	        vec3 rgb = min ( sumOfLights, vec3(1.0, 1.0, 1.0) ); //clamp lighting at all white
            vec4 fcolor = vec4(rgb.r, rgb.g, rgb.b, color.a); //use the fragment's original alpha
            frag_color = fcolor;
        }
    </script>

    <script id="toon-vertex-shader" type="shader.vert">
        #version 300 es //specifies that we want OpenGL ES 3.0
        //This vertex shader is based on Example 7.8 on pages 377 and 378 of the OpenGL Programming Guide (8th edition)
        //I modified the code somewhat to make it closer to the Phong Reflection Model discussed in COMP 153.
        //For lab 7 it was modified to allow the eye position to move around the scene. Lighting calculations 
        //are done in "eye" coordinates, so position and normal must be calculated appropriately.

        layout(location = 0) in vec4 vertexPosition;
        layout(location = 1) in vec4 vertexColor;
        layout(location = 2) in vec3 vertexNormal;

        uniform mat4 modelMatrix;
        uniform mat4 viewMatrix;
        uniform mat4 projectionMatrix;

        out vec4 position; //position of the vertex in "eye" coordinates
        out vec4 color;
        out vec3 normal; //orientation of the normal in "eye" coordinates

        void main()
        {
            //Assign the in variables (attributes) to out variables (varyings) so that
            //they can be accessed in the fragment shader for lighting calculations.
            position = viewMatrix * modelMatrix * vertexPosition; //position in "eye" coordinates
            color = vertexColor;
            //calculate the "normal matrix"
            mat4 modelViewMatrix = viewMatrix * modelMatrix;
            mat4 normalMatrix = transpose(inverse(modelViewMatrix));
            vec4 n = normalMatrix * vec4(vertexNormal, 0.0);
            normal = normalize(vec3(n.x, n.y, n.z)); //normalize just in case it is not a unit vector

            //Here the input vertexPostion is multiplied by the model, view, and
            //projection matrices to determine the final position of the vertex
            gl_Position = projectionMatrix * viewMatrix * modelMatrix * vertexPosition;
        }
    </script>
    <script id="toon-fragment-shader" type="shader.frag">
        #version 300 es //specifies that we want OpenGL ES 3.0
        precision highp float; //specifies "high precision" - necessary to access the viewMatrix here
        //The toon shader part of this code is adapted from: 
        // www.lighthouse3d.com/tutorials/glsl-tutorial/toon-shading-version-iii/

        //uniform variables
        uniform vec3 globalAmbientLight;
        uniform mat4 viewMatrix; //needed to transform the light positions into "eye" coordinates
        uniform vec4 light_color; //color of light
        uniform vec4 light_position; //location of light if w = 1, otherwise the direction towards the light

        in vec4 position; //position of the fragment in "eye" coordinates
        in vec4 color; //surface color at the fragment
        in vec3 normal; //normal vector at the fragment in "eye" coordinates

        out vec4 frag_color; //resulting fragment color after lighting is applied

        void main()
        {
            //Implement a toon shader for lighting
            vec3 colorSum = vec3(0.0, 0.0, 0.0);
            vec3 n = normalize(normal);
            vec3 L; //this will be the vector towards the light
            vec3 I = light_color.rgb;  //Intensity (color) of the light
            vec4 lightPos_eyeCoords = viewMatrix*light_position; //put light_position in "eye" coordinates

            if (light_position.w > 0.1) //a positional light
            {
                vec4 LTemp = lightPos_eyeCoords - position;  //Calculate the L vector
                L = normalize(vec3(LTemp.x, LTemp.y, LTemp.z)); //remove the w value from the L vector and normalize
            }
            else //a directional light
            {
                //remove w value to form the L vector and normalize
                L = normalize(vec3(lightPos_eyeCoords.x, lightPos_eyeCoords.y, lightPos_eyeCoords.z));
            }

            float intensity = dot(L,n);
            if (intensity > 0.95)
                colorSum += color.rgb * I * 1.0;
            else if (intensity > .75)
                colorSum += color.rgb * I * 0.75;
            else if (intensity > .5)
                colorSum += color.rgb * I * 0.5;
            else if (intensity > 0.25)
                colorSum += color.rgb * I * 0.25;
            else
                colorSum += color.rgb * I * 0.05;

            colorSum = colorSum + globalAmbientLight*color.rgb;  //compute the final color of the fragment
            frag_color = vec4(colorSum.r, colorSum.g, colorSum.b, color.a);  //use the fragment's original alpha
        }
    </script>

    <script id="point-sprite-vertex-shader" type="shader.vert">
        #version 300 es
        precision mediump float;
        //See the section of the textbook titled "Point Sprites" for a discussion of point sprites
         
        layout(location = 0) in vec4 vertexPosition;

        //uniform variables
        uniform mat4 modelMatrix;
        uniform mat4 viewMatrix;
        uniform mat4 projectionMatrix;
     
        void main(void) 
        {  
            gl_PointSize = 150.0;  //NOTE: the value assigned to gl_PointSize must be a float
            gl_Position = projectionMatrix * viewMatrix * modelMatrix * vertexPosition;
            //TODO: Comment out the two lines of code above and add the three lines below to scale size base on distance from the eye
            vec4 pos = projectionMatrix * viewMatrix * modelMatrix * vec4(vertexPosition.x, vertexPosition.y, vertexPosition.z, 1.0);
            //gl_PointSize = (1.0 - pos.z / pos.w) * 1000.0; //set gl_PointSize for point sprites as a function of the distance from viewer
            //gl_Position = pos;
        }
    </script>
    <script id="point-sprite-fragment-shader" type="shader.frag">
        #version 300 es //specifies that we want OpenGL ES 3.0
        precision highp float; //specifies "high precision"
        //See the section of the textbook titled "Point Sprites" for a discussion of point sprites

        uniform sampler2D tex_image; //Support for texture mapping

        out vec4 frag_color; //resulting fragment color after the point sprite texture is applied

        void main()
        {
            float threshold = 0.15;
            vec4 color = texture(tex_image, gl_PointCoord);
            frag_color = color;
            //TODO: Add the two lines below to make the texture transparent where it is black (or at least very dark gray)
            if (color.r <= threshold && color.g <= threshold && color.b <= threshold)
                discard; //pretend this fragment doesn't exist (i.e. make it transparent)
        }
    </script>

    <!-- Call our controller function from main once the document has loaded -->
    <script type="text/javascript">
        function main()
        {
            controller(); //controller is defined in controller.js
        }
        window.onload = main; 
    </script>
</head>
<body>
    <canvas id="webgl-canvas" width="900" height="600">
        Your browser does not support the HTML5 canvas element.
    </canvas>
</body>
</html>